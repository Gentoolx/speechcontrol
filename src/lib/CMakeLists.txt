project(SpeechControlLibrary)
cmake_minimum_required(VERSION 2.8)

set(CMAKE_INCLUDE_CURRENT_DIR ON)

## setting up sources
file(GLOB SPCHCNTRL_LIB_SOURCES "*.cpp")
file(GLOB SPCHCNTRL_LIB_HEADERS "*.hpp")

## variables
set(SPCHCNTRL_LIB_MAJOR_VERSION 0)
set(SPCHCNTRL_LIB_MINOR_VERSION 1)
set(SPCHCNTRL_LIB_VERSION "${SPCHCNTRL_LIB_MAJOR_VERSION}.${SPCHCNTRL_LIB_MINOR_VERSION}")
set(SPCHCNTRL_LIB_COMPILE_FLAGS ${SPCHCNTRL_COMPILE_FLAGS})
set(SPCHCNTRL_LIB_COMPILE_FLAGS_DEBUG ${SPCHCNTRL_COMPILE_FLAGS_DEBUG})
set(SPCHCNTRL_LIB_COMPILE_DEFINITIONS ${SPCHCNTRL_COMPILE_DEFINITIONS})
set(SPCHCNTRL_LIB_INCLUDE_DIR "${SPCHCNTRL_INCLUDE_DIR}/lib")
set(SPCHCNTRL_LIB_INCLUDE_DIRS ${SPCHCNTRL_INCLUDE_DIRS})
set(SPCHCNTRL_LIB_LIBRARIES ${SPCHCNTRL_LIBRARIES})

if (CMAKE_BUILD_TYPE STREQUAL "Debug")
    set(SPCHCNTRL_LIB_COMPILE_FLAGS "${SPCHCNTRL_LIB_COMPILE_FLAGS} ${SPCHCNTRL_LIB_COMPILE_FLAGS_DEBUG}")
endif (CMAKE_BUILD_TYPE STREQUAL "Debug")

## Test if we're going to make Python bindings.
if(WITH_PYTHON_BINDINGS)
    message(STATUS "Python bindings for libspeechcontrol enabled.")
    find_package(PythonLibs 2.7 REQUIRED)
    find_package(Boost 1.46 COMPONENTS python REQUIRED)

    if (Boost_FOUND AND PYTHONLIBS_FOUND)
        list(APPEND SPCHCNTRL_LIB_INCLUDE_DIRS ${Boost_INCLUDE_DIRS} ${PYTHON_INCLUDE_DIRS})
        list(APPEND SPCHCNTRL_LIB_LIBRARIES ${Boost_LIBRARIES} ${PYTHON_LIBRARIES})
        list(APPEND SPCHCNTRL_LIB_COMPILE_DEFINITIONS WITH_PYTHON_BINDINGS)
    endif (Boost_FOUND AND PYTHONLIBS_FOUND)
else(WITH_PYTHON_BINDINGS)
    message(STATUS "Python bindings for libspeechcontrol disabled.")
endif(WITH_PYTHON_BINDINGS)

## configures
configure_file("${PROJECT_SOURCE_DIR}/config.hpp.in"
    "${PROJECT_SOURCE_DIR}/config.hpp" @ONLY)

## Some Qt-specific stuff.
QT4_AUTOMOC(${SPCHCNTRL_LIB_SOURCES})

## Define the target
add_library(speechcontrol SHARED ${SPCHCNTRL_LIB_SOURCES})
set_target_properties(speechcontrol PROPERTIES
    COMPILE_FLAGS "${SPCHCNTRL_LIB_COMPILE_FLAGS}"
    COMPILE_DEFINITIONS ${SPCHCNTRL_LIB_COMPILE_DEFINITIONS}
    DEFINE_SYMBOL "SPCH_EXPORT_SYMBOLS"
    VERSION "${SPCHCNTRL_LIB_VERSION}"
    SOVERSION "${SPCHCNTRL_LIB_VERSION}")

target_link_libraries(speechcontrol ${SPCHCNTRL_LIB_LIBRARIES})

## Install stuff.
install(TARGETS speechcontrol
    EXPORT SpeechControlLibraryDepends
    LIBRARY DESTINATION "${SPCHCNTRL_LIB_DIR}")

install(EXPORT SpeechControlLibraryDepends
    DESTINATION "${SPCHCNTRL_CMAKE_DIR}"
    COMPONENT "dev")

install(FILES ${SPCHCNTRL_LIB_HEADERS}
    DESTINATION "${SPCHCNTRL_LIB_INCLUDE_DIR}"
    COMPONENT "dev")

include_directories(${SPCHCNTRL_LIB_INCLUDE_DIRS}
    "${PROJECT_SOURCE_DIR}")

add_subdirectory(cmake)